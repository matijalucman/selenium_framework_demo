group 'com.ml.demo.selenium_gradle_tests'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply from: "closures.gradle"
apply from: "constants.gradle"

sourceCompatibility = 1.8

loadProperties()

configurations {
    antClasspath
}

repositories {
    mavenCentral()
}

sourceSets {
    test {
        java {
            srcDirs = ['src/main/java']
        }
    }
}

dependencies {
    // Selenium
    compile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: ver.selenium
    compile group: 'org.seleniumhq.selenium', name: 'selenium-server', version: ver.selenium
    // TestNG & AssertJ
    compile group: 'org.testng', name: 'testng', version: ver.testNG
    compile group: 'org.assertj', name: 'assertj-core', version: ver.assertJ
    // Cucumber & Gherkin
    compile group: 'info.cukes', name: 'gherkin', version: ver.gherkin
    compile group: 'info.cukes', name: 'cucumber-java', version: ver.cucumber
    compile group: 'info.cukes', name: 'cucumber-testng', version: ver.cucumber
    compile group: 'info.cukes', name: 'cucumber-picocontainer', version: ver.cucumber
    // Ant email
    antClasspath group: 'javax.mail', name: 'mail', version: ver.javaxMail
    antClasspath group: 'ant', name: 'ant-javamail', version: ver.antJavaMail
    antClasspath group: 'javax.activation', name: 'activation', version: ver.javaxActivatio
    // Cucumber Extent report
    compile group: 'com.aventstack', name: 'extentreports', version: ver.extentreport
    compile group: 'com.vimalselvam', name: 'cucumber-extentsreport', version:  ver.cucumberreport
}

task runTests(type: Test) {
    // Ignore test failures
    ignoreFailures = true

    // Turn off Gradle HTML report
    reports.html.enabled = false

    // Execute test even if code is up to date
    outputs.upToDateWhen { false }

    // Set cucumber options as system variable
    systemProperty 'cucumber.options', cucumberOptions

    doFirst {
        // Turn on grid
        startHub()
        startNode()

        useTestNG() {
            // Set TestNG xml suite
            suites profileProperties.testSuite

            // Exclude browsers from execution
            excludeGroups excludeBrowsers
        }
    }

    doLast {
        // Turn off grid
        stopHub()
        stopNode()

        // Send email with results
//        sendEmail()
    }
}
